// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:

    Mux16(a=x, b[0]=false, sel=zx, out=x1);
    Not16(in=x1, out = notX1);
    Mux16(a=x1, b=notX1, sel=nx, out = x2);

    Mux16(a=y, b[0]=false, sel=zy, out = y1);
    Not16(in = y1, out = notY);
    Mux16(a=y1, b=notY, sel=ny, out = y2);

    Add16(a=x2, b=y2, out=aPlusB);
    And16(a=x2, b=y2, out=aAndB);
    Mux16(a=aAndB, b=aPlusB, sel=f, out = fOut);

    Not16(in = fOut, out = notF);   
    Mux16(a=fOut, b=notF, sel=no,out[15] = neg, out[0..7]= res07,out[8..15] = res815, out = out);

    Or8Way(in=res07, out = or07);
    Or8Way(in=res815, out = or815);

    Not(in=or07, out=not07);
    Not(in=or815, out=not815);

    And(a=not07, b=not815, out = zr);

    Not(in=neg, out=notNeg);
    Not(in=notNeg, out=ng);
}